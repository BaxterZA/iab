apply plugin: 'com.android.library'

android {
    compileSdkVersion 26



    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:27.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation "org.mockito:mockito-android:2.10.0"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:3.6.0'
    androidTestImplementation ('com.android.support.test.espresso:espresso-core:3.0.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def javaDocDir = new File(rootDir, 'docs')

task prepareJavaDocDir() {
    doFirst {
        if (javaDocDir.exists()) {
            javaDocDir.deleteDir()
        }
        javaDocDir.mkdirs()
    }
}

task generateJavaDocs(dependsOn: [prepareJavaDocDir, 'bundleDebug'], type: Javadoc, group: 'doc') {
    source = android.sourceSets.main.java.srcDirs
    title = "Appodeal IAB SDK"
    destinationDir = javaDocDir
    classpath += files("${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar")
    classpath += files("${projectDir}/build/intermediates/bundles/debug/classes.jar")
    exclude "com/appodeal/iab/mraid"
    exclude "com/appodeal/iab/vast"
    exclude "com/appodeal/iab/views"
    exclude "com/appodeal/iab/vpaid"
    exclude "com/appodeal/iab/webview"
    failOnError = false
    options.use = false
    options.author = false
    options.version = false
    options.noDeprecated
    options.addStringOption('Xdoclint:none', '-quiet')
    options.memberLevel = JavadocMemberLevel.PUBLIC
    options {
        links "http://docs.oracle.com/javase/7/docs/api/"
        linksOffline "https://developer.android.com/reference","${android.sdkDirectory}/docs/reference"
    }
}

afterEvaluate {
    generateJavaDocs.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompiler.classpath.files
    })
}
